Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ВидНоменклатуры = Перечисления.ВидНоменклатуры.Услуга Тогда
		// Если вид номенклатуры Услуга, то должны быть прикреплены необходимые материалы
		Если Материалы.Количество() = 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнены материалы, которые требуются для оказания услуги";
			Сообщение.Поле = "Материалы";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();

			Отказ = Истина;
		Иначе
			// Проверяем, что в списке материалов находятся только материалы
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Материалы.НомерСтроки,
			|	ВЫРАЗИТЬ(Материалы.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
			|ПОМЕСТИТЬ ВТ_Материалы
			|ИЗ
			|	&Материалы КАК Материалы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТ_Материалы.НомерСтроки,
			|	ВТ_Материалы.Номенклатура
			|ИЗ
			|	ВТ_Материалы КАК ВТ_Материалы
			|ГДЕ
			|	ВТ_Материалы.Номенклатура.ВидНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ВидНоменклатуры.Материал)";

			Запрос.Параметры.Вставить("Материалы", Материалы);
			РезультатЗапроса = Запрос.Выполнить();

			Выборка = РезультатЗапроса.Выбрать();

			Пока Выборка.Следующий() Цикл
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Материал ""%1"" должен иметь только тип ""Материал""",
					Выборка.Номенклатура);
				Сообщение.Поле = СтрШаблон("Материалы[%1].Номенклатура", Выборка.НомерСтроки);
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();

				Отказ = Истина;
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ВидНоменклатуры = Перечисления.ВидНоменклатуры.Материал Тогда
		// Если материал, то материалов не должно быть
		Если Материалы.Количество() <> 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Нельзя заполнять список материалов для номенклатуры с типом ""Материал""";
			Сообщение.Поле = "Материалы";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();

			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры