// Получить цены для номенклатуры.
// 
// Параметры:
//  СписокНоменклатуры - ТабличнаяЧасть - Список номенклатуры
//  Период - Дата - Период
//  СтолбецНоменклатуры - Строка - Столбец номенклатуры
//  СтолбецЦены - Строка - Стобец цены
//  СтолбецКоличество - Строка - Столбец количество
//  СтолбецСуммы - Строка - Столбец суммы
Процедура ПолучитьЦеныДляНоменклатуры(СписокНоменклатуры, Период, СтолбецНоменклатуры, СтолбецЦены, СтолбецКоличество,
	СтолбецСуммы) Экспорт
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокНоменклатуры.%1 КАК Номенклатура,
	|	СписокНоменклатуры.%2 КАК Количество,
	|	СписокНоменклатуры.НомерСтроки
	|ПОМЕСТИТЬ ВТ_СписокНоменклатуры
	|ИЗ
	|	&СписокНоменклатуры КАК СписокНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СписокНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ЦеныНоменклатурыСрезПоследних.Цена * ВТ_СписокНоменклатуры.Количество КАК Сумма
	|ИЗ
	|	ВТ_СписокНоменклатуры КАК ВТ_СписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период,) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВТ_СписокНоменклатуры.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Запрос.Текст = СтрШаблон(ТекстЗапроса, СтолбецНоменклатуры, СтолбецКоличество);

	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры.Выгрузить());
	Запрос.УстановитьПараметр("Период", Период);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокНоменклатуры.Получить(Выборка.НомерСтроки - 1);
		НоваяСтрока[СтолбецЦены] = Выборка.Цена;
		НоваяСтрока[СтолбецСуммы] = Выборка.Сумма;
	КонецЦикла;
КонецПроцедуры