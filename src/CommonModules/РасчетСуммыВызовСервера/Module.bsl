// Рассчитать сумму услуг в списке этапов.
// 
// Параметры:
//  СписокНоменклатуры - ТабличнаяЧасть - Список номенклатуры
//  СтолбецЦены - Строка - Столбец цены
//  СтолбецКоличества - Строка - Столбец количества
//  СтолбецСуммы - Строка - Столбец суммы
Процедура РассчитатьСуммуПоЦенеИКоличеству(СписокНоменклатуры, СтолбецЦены, СтолбецКоличества, СтолбецСуммы) Экспорт
	Для Каждого Строка Из СписокНоменклатуры Цикл
		Строка[СтолбецСуммы] = Строка[СтолбецЦены] * Строка[СтолбецКоличества];
	КонецЦикла;
КонецПроцедуры

// Рассчитать стоимость этапов в заказе клиента.
// 
// Параметры:
//  ЗаказКлиента - ДокументОбъект.ЗаказКлиента - Заказ клиента
Процедура РассчитатьСтоимостьЭтаповВЗаказеКлиента(ЗаказКлиента) Экспорт
	// 1. Выбираем список этапов
	// 2. Выбираем список материалов
	// 3. Группируем список материалов по этапам
	// 4. Соединяем этапы и материалы, получаем стоимость этапа
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокЭтапов.НомерСтроки,
	|	СписокЭтапов.ЦенаУслуги * СписокЭтапов.Количество КАК СуммаУслуги
	|ПОМЕСТИТЬ ВТ_СписокЭтапов
	|ИЗ
	|	&СписокЭтапов КАК СписокЭтапов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокМатериалов.НомерЭтапа,
	|	СписокМатериалов.Сумма
	|ПОМЕСТИТЬ ВТ_СписокМатериалов
	|ИЗ
	|	&СписокМатериалов КАК СписокМатериалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СписокМатериалов.НомерЭтапа,
	|	СУММА(ВТ_СписокМатериалов.Сумма) КАК СуммаМатериалов
	|ПОМЕСТИТЬ ВТ_СписокМатериаловПоЭтапам
	|ИЗ
	|	ВТ_СписокМатериалов КАК ВТ_СписокМатериалов
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СписокМатериалов.НомерЭтапа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СписокЭтапов.НомерСтроки КАК НомерСтроки,
	|	ВТ_СписокЭтапов.СуммаУслуги + ЕстьNULL(ВТ_СписокМатериаловПоЭтапам.СуммаМатериалов, 0) КАК СтоимостьЭтапа,
	|	ВТ_СписокЭтапов.СуммаУслуги
	|ИЗ
	|	ВТ_СписокЭтапов КАК ВТ_СписокЭтапов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокМатериаловПоЭтапам КАК ВТ_СписокМатериаловПоЭтапам
	|		ПО ВТ_СписокЭтапов.НомерСтроки = ВТ_СписокМатериаловПоЭтапам.НомерЭтапа
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Запрос.УстановитьПараметр("СписокЭтапов", ЗаказКлиента.СписокЭтапов.Выгрузить());
	Запрос.УстановитьПараметр("СписокМатериалов", ЗаказКлиента.СписокМатериалов.Выгрузить());

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ЗаказКлиента.СписокЭтапов.Получить(Выборка.НомерСтроки - 1);
		НоваяСтрока.СуммаУслуги = Выборка.СуммаУслуги;
		НоваяСтрока.СтоимостьЭтапа = Выборка.СтоимостьЭтапа;
	КонецЦикла;
КонецПроцедуры