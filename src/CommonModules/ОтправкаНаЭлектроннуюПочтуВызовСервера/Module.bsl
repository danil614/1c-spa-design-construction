Функция ОтправитьПисьмо(УчетнаяЗапись, Получатель, ТемаПисьма, ТекстПисьма, ОписаниеОшибки, Вложения = Неопределено,
	КопияПолучателя = Неопределено) Экспорт
	Профиль = Новый ИнтернетПочтовыйПрофиль;
	Профиль.АдресСервераSMTP = УчетнаяЗапись.АдресСервераSMTP;
	Профиль.ПользовательSMTP = УчетнаяЗапись.ПользовательSMTP;
	Профиль.ПарольSMTP = УчетнаяЗапись.ПарольSMTP;
	Профиль.ПортSMTP = УчетнаяЗапись.ПортSMTP;
	Профиль.ИспользоватьSSLSMTP = УчетнаяЗапись.ИспользоватьSSLSMTP;

	Почта = Новый ИнтернетПочта;
	Попытка
		Почта.Подключиться(Профиль);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;

	Сообщение = Новый ИнтернетПочтовоеСообщение;

	Сообщение.Получатели.Добавить(Получатель);

	Если ЗначениеЗаполнено(КопияПолучателя) Тогда
		Сообщение.Копии.Добавить(КопияПолучателя);
	КонецЕсли;

	Сообщение.Отправитель.Адрес = Профиль.ПользовательSMTP;
	Сообщение.Тема = ТемаПисьма;
	Сообщение.Тексты.Добавить(ТекстПисьма);

	Если ЗначениеЗаполнено(Вложения) Тогда
		Для Каждого Вложение Из Вложения Цикл
			Сообщение.Вложения.Добавить(Вложение.ДвоичныеДанные, Вложение.НаименованиеВложения);
		КонецЦикла;
	КонецЕсли;

	Попытка
		Почта.Послать(Сообщение);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;

	Почта.Отключиться();
	Возврат Истина;
КонецФункции

Функция ПолучитьВложениеПоТабличномуДокументу(ТабличныйДокумент, НазваниеДокумента, ТипДокумента) Экспорт
	ПотокФайла = Новый ПотокВПамяти;
	ТабличныйДокумент.Записать(ПотокФайла, ТипДокумента);
	ДвоичныеДанныеФайла = ПотокФайла.ЗакрытьИПолучитьДвоичныеДанные();

	Возврат Новый Структура("ДвоичныеДанные,НаименованиеВложения", ДвоичныеДанныеФайла, НазваниеДокумента);
КонецФункции