#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокЭтапов

&НаКлиенте
Процедура СписокЭтаповКоличествоПриИзменении(Элемент)
	РассчитатьСуммуУслугиПоЦенеИКоличеству();
КонецПроцедуры

&НаКлиенте
Процедура СписокЭтаповЦенаУслугиПриИзменении(Элемент)
	РассчитатьСуммуУслугиПоЦенеИКоличеству();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуУслугиПоЦенеИКоличеству()
	Если Элементы.СписокЭтапов.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = Элементы.СписокЭтапов.ТекущиеДанные;
		Строка = Объект.СписокЭтапов.Получить(ТекущиеДанные.НомерСтроки - 1);
		Строка.СуммаУслуги = ТекущиеДанные.ЦенаУслуги * ТекущиеДанные.Количество;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокМатериалов

&НаКлиенте
Процедура СписокМатериаловКоличествоПриИзменении(Элемент)
	РассчитатьСуммуМатериалаПоЦенеИКоличеству();
КонецПроцедуры

&НаКлиенте
Процедура СписокМатериаловЦенаПриИзменении(Элемент)
	РассчитатьСуммуМатериалаПоЦенеИКоличеству();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуМатериалаПоЦенеИКоличеству()
	Если Элементы.СписокМатериалов.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = Элементы.СписокМатериалов.ТекущиеДанные;
		Строка = Объект.СписокМатериалов.Получить(ТекущиеДанные.НомерСтроки - 1);
		Строка.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура СформироватьСписокМатериалов(Команда)
	Если Объект.СписокМатериалов.Количество() > 0 Тогда
		ТекстВопроса = "При формировании списка материалов текущий список будет очищен! Продолжить?";
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);

		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	СформироватьСписокМатериаловНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦеныНаУслуги(Команда)
	УстановитьЦеныНаУслугиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦеныНаМатериалы(Команда)
	УстановитьЦеныНаМатериалыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуЭтапов(Команда)
	// Сначала рассчитаем сумму материалов
	РассчитатьСуммуМатериаловНаСервере();
	
	// Далее рассчитаем стоимость всех этапов
	РассчитатьСуммуЭтаповНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуМатериалов(Команда)
	РассчитатьСуммуМатериаловНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуЗаказа(Команда)
	РассчитатьСуммуЗаказаНаСервере();
КонецПроцедуры

#КонецОбласти
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьСписокМатериаловНаСервере()
	// Очищаем список материалов
	Объект.СписокМатериалов.Очистить();

	// Выбираем списки материалов из услуг
	// Перемножаем количество
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокЭтапов.Услуга,
	|	СписокЭтапов.Количество,
	|	СписокЭтапов.НомерСтроки
	|ПОМЕСТИТЬ ВТ_СписокЭтапов
	|ИЗ
	|	&СписокЭтапов КАК СписокЭтапов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураМатериалы.Номенклатура КАК Материал,
	|	НоменклатураМатериалы.Количество * ВТ_СписокЭтапов.Количество КАК Количество,
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ВТ_СписокЭтапов.НомерСтроки КАК НомерЭтапа
	|ИЗ
	|	ВТ_СписокЭтапов КАК ВТ_СписокЭтапов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.Материалы КАК НоменклатураМатериалы
	|		ПО ВТ_СписокЭтапов.Услуга = НоменклатураМатериалы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период,) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО НоменклатураМатериалы.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_СписокЭтапов.НомерСтроки,
	|	НоменклатураМатериалы.НомерСтроки";

	Запрос.УстановитьПараметр("СписокЭтапов", Объект.СписокЭтапов.Выгрузить());
	Запрос.УстановитьПараметр("Период", ПолучитьДатуДокумента());
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СписокМатериалов.Добавить();
		НоваяСтрока.НомерЭтапа = Выборка.НомерЭтапа;
		НоваяСтрока.Материал = Выборка.Материал;
		НоваяСтрока.Количество = Выборка.Количество;
		НоваяСтрока.Цена = Выборка.Цена;
		НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьЦеныНаУслугиНаСервере()
	ПолучениеЦенНоменклатурыВызовСервера.ПолучитьЦеныДляНоменклатуры(Объект.СписокЭтапов, ПолучитьДатуДокумента(),
		"Услуга", "ЦенаУслуги", "Количество", "СуммаУслуги");
КонецПроцедуры

&НаСервере
Процедура УстановитьЦеныНаМатериалыНаСервере()
	ПолучениеЦенНоменклатурыВызовСервера.ПолучитьЦеныДляНоменклатуры(Объект.СписокМатериалов, ПолучитьДатуДокумента(),
		"Материал", "Цена", "Количество", "Сумма");
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуЭтаповНаСервере()
	РасчетСуммыВызовСервера.РассчитатьСтоимостьЭтаповВЗаказеКлиента(Объект);
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуМатериаловНаСервере()
	РасчетСуммыВызовСервера.РассчитатьСуммуПоЦенеИКоличеству(Объект.СписокМатериалов, "Цена", "Количество", "Сумма");
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуЗаказаНаСервере()
	// Сначала рассчитаем сумму материалов
	РассчитатьСуммуМатериаловНаСервере();
	
	// Далее рассчитаем стоимость всех этапов
	РассчитатьСуммуЭтаповНаСервере();

	Объект.СуммаЗаказа = Объект.СписокЭтапов.Итог("СтоимостьЭтапа");
КонецПроцедуры

&НаСервере
Функция ПолучитьДатуДокумента()
	Возврат ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
КонецФункции

#КонецОбласти