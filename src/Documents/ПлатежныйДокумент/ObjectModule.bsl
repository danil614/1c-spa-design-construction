Процедура ОбработкаПроведения(Отказ, Режим)
	// Регистр ДенежныеСредства
	Движения.ДенежныеСредства.Записывать = Истина;

	Движение = Движения.ДенежныеСредства.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.ЗаказКлиента = ЗаказКлиента;
	Движение.Сумма = СуммаОплаты;

	Движения.Записать();

	// Проверяем сумму оплаты
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ДенежныеСредстваОбороты.СуммаПриход, 0) КАК СуммаОплачено
	|ПОМЕСТИТЬ ВТ_СуммаОплат
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.Обороты(,,, ЗаказКлиента = &Ссылка) КАК ДенежныеСредстваОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ДокументЗаказКлиента.СуммаЗаказа, 0) КАК СуммаЗаказа,
	|	ЕСТЬNULL(ВТ_СуммаОплат.СуммаОплачено, 0) КАК СуммаОплачено
	|ИЗ
	|	Документ.ЗаказКлиента КАК ДокументЗаказКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммаОплат КАК ВТ_СуммаОплат
	|		ПО ИСТИНА
	|ГДЕ
	|	ДокументЗаказКлиента.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", ЗаказКлиента);
	Запрос.УстановитьПараметр("Период", МоментВремени());

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Если Выборка.Следующий() Тогда
		СуммаОплачено = Выборка.СуммаОплачено;
		СуммаЗаказа = Выборка.СуммаЗаказа;
	Иначе
		СуммаОплачено = 0;
		СуммаЗаказа = 0;
	КонецЕсли;

	Если СуммаОплачено > СуммаЗаказа Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(
				"Оплаченная сумма (%1 ₽) больше суммы заказа (%2 ₽)", СуммаОплачено, СуммаЗаказа);
		Сообщение.Поле = "СуммаОплаты";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();

		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры